GeoJSON               -> { GJProperties }
GJProperties          -> GJProperty GJPropertiesTail
GJPropertiesTail      -> , GJProperty GJPropertiesTail | null
GJProperty            -> "type" : TypeVal 
                      | "features" : [ FeatureValues ] 
                      | "geometry" : { GProperties }
                      | "properties" : { GenericProperties }
                      | "coordinates" : [ CoordinatesRoot ]
                      | "geometries" : [ GeometryValues ]
                      | GenericProperty

GeometryType          -> "Point" 
                      | "LineString" 
                      | "Polygon" 
                      | "MultiPoint" 
                      | "MultiLineString" 
                      | "MultiPolygon" 
                      | "GeometryCollection"

TypeVal               -> "FeatureCollection" 
                      | "Feature" 
                      | GeometryType

GenericProperties     -> GenericProperty GenericPropertiesTail
GenericPropertiesTail -> , GenericProperty GenericPropertiesTail | null
GenericProperty       -> STRING : Value

FeatureValues         -> { FProperties } FeatureValuesTail | null
FeatureValuesTail     -> , { FProperties } FeatureValuesTail | null

GeometryValues        -> { GProperties } GeometryValuesTail | null
GeometryValuesTail    -> , { GProperties } GeometryValuesTail | null

FProperties           -> FProperty FPropertiesTail
FPropertiesTail       -> , FProperty FPropertiesTail | null
FProperty             -> "type" : "Feature" 
                      | "geometry" : { GProperties } 
                      | "properties" : { GenericProperties } 
                      | GenericProperty

GProperties           -> GProperty GPropertiesTail
GPropertiesTail       -> , GProperty GPropertiesTail | null
GProperty             -> "type" : GeometryType 
                      | "coordinates" : [ CoordinatesRoot ]
                      | "geometries" : [ GeometryValues ]
                      | GenericProperty

CoordinatesRoot       -> [ CoordinatesRoot ] CoordinatesRootTail | Coordinates
CoordinatesRootTail   -> , [ CoordinatesRoot ] CoordinatesRootTail | null
Coordinates           -> NUMBER CoordinatesTail | null
CoordinatesTail       -> , NUMBER CoordinatesTail | null

Value                 -> "true" 
                      | "false" 
                      | "null" 
                      | { GenericProperties } 
                      | [ GenericArrayValues ] 
                      | STRING 
                      | NUMBER 

GenericArrayValues    -> Value GenericArrayValuesTail
GenericArrayValuesTail-> , Value GenericArrayValuesTail | null